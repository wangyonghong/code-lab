pluginManagement {
    // 参考：
    // https://juejin.cn/post/6844903941793120270  Gradle 插件
    // https://www.cnblogs.com/baiyuas/p/15171076.html  AndroidStudio Gradle编译脚本中关于插件部分的变更
    repositories {
        gradlePluginPortal()
        google()
        maven {
            url 'https://mirrors.cloud.tencent.com/nexus/repository/maven-public/'
        }
        mavenCentral()
        maven {
            url uri('./repo')
        }
    }

    // 这里感觉有些奇怪，这里写完版本号之后，在项目根目录 build.gradle 中还要声明
    // AGP（Android Gradle plugin） 和 Gradle version 版本兼容性
    // https://developer.android.com/studio/releases/gradle-plugin
    plugins {
        id 'com.android.application' version '7.1.3'
        id 'com.android.library' version '7.1.3'
        id 'com.jakewharton.butterknife' version '10.2.3'
        id 'com.sensorsdata.android.appclick5' version '1.0.0-SNAPSHOT'
        id 'com.sensorsdata.android.appclick6' version '1.0.0-SNAPSHOT'
        id 'com.sensorsdata.android.appclick7' version '1.0.0-SNAPSHOT'
    }

    // 以前可以用 classpath 的方式，现在需要用 resolutionStrategy
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'com.jakewharton.butterknife') {
                useModule("com.jakewharton:butterknife-gradle-plugin:10.2.3")
            }
            if (requested.id.id == 'com.sensorsdata.android.appclick5') {
                useModule("me.yonghong:demo-autotrack-click5:1.0.0-SNAPSHOT")
            }
            if (requested.id.id == 'com.sensorsdata.android.appclick6') {
                useModule("me.yonghong:demo-autotrack-click6:1.0.0-SNAPSHOT")
            }
            if (requested.id.id == 'com.sensorsdata.android.appclick7') {
                useModule("me.yonghong:demo-autotrack-click7:1.0.0-SNAPSHOT")
            }

        }
    }
}

dependencyResolutionManagement {
    // https://blog.csdn.net/a740169405/article/details/121264449
    // 这里有个报错所以先注释掉了，repository ‘Gradle Libs‘ was added by unknown code
    // repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        maven {
            url 'https://mirrors.cloud.tencent.com/nexus/repository/maven-public/'
        }
        mavenCentral()
        google()
        maven {
            url uri('repo')
        }
    }
}


include ':ViewScreen', ':DemoViewScreen'
include ':StartEnd', ':DemoStartEnd'
include ':AppClick1', ':DemoAppClick1'
include ':AppClick2', ':DemoAppClick2'
include ':AppClick3', ':DemoAppClick3'
include ':AppClick4', ':DemoAppClick4'
include ':pluginAppClick5', ':AppClick5'
include ':pluginAppClick6', ':AppClick6'
include ':pluginAppClick7', ':AppClick7'
include ':pluginAppClick8', ':AppClick8'
// 如果项目导入失败。可以先注释掉下面这行，先发布插件
include ':DemoAppClick5', ':DemoAppClick6', ':DemoAppClick7', ':DemoAppClick8'

//include ':sdk', ':app'